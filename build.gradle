group = 'com.yahoo.parsec.base-build'

buildscript{
    repositories{
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        jcenter()
    }

    dependencies{
        classpath group: 'com.jfrog.bintray.gradle' , name: 'gradle-bintray-plugin', version: '1.6'
        classpath group: 'net.researchgate'         , name: 'gradle-release'       , version: '2.4.0'
    }
}

apply plugin: 'groovy'
apply plugin: 'java'
apply plugin: 'net.researchgate.release'
apply plugin: 'com.jfrog.bintray'


bintray {
    user = bintrayUser
    key = bintrayKey

    filesSpec {
        from "./src/main/resources"
        into "./${bintrayPackage}/${project.properties['release.releaseVersion']}"
    }

    publish = true

    pkg {
        repo = bintrayRepo
        name = bintrayPackage
        licenses = ['Apache-2.0']
        vcsUrl = bintrayVcsURL

        version {
            name = project.properties['release.releaseVersion']
        }
    }
}

release {
    failOnUnversionedFiles = false
}

updateVersion << {
    def readme = new File('./README.md')
    def releaseVersion = project.property("release.releaseVersion")
    ant.replaceregexp(file: readme, match: "/parsec-base-build/\\S+/parsec.gradle", replace: "/parsec-base-build/${releaseVersion}/parsec.gradle")
}

beforeReleaseBuild.dependsOn check
afterReleaseBuild.dependsOn bintrayUpload


repositories{
    mavenCentral()
    jcenter()
}

dependencies{
    testCompile gradleTestKit()
    testCompile group: 'org.codehaus.groovy', name: 'groovy-all', version: '2.4.4'
    testCompile group: 'org.spockframework' , name: 'spock-core', version: '1.0-groovy-2.4'
    testCompile group: 'junit'              , name: 'junit'     , version: '4.12'
}